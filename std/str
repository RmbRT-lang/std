(/rl/)

INCLUDE 'std/buffer'
INCLUDE 'std/heap'

::std::str
{
	[C: TYPE]
	View -> std::[C!]Buffer
	{
		{str: C! *} -> (str, std::str::len(str));
		:view{str: C! *} -> (str, std::str::len(str));
		:buf{str: C! *, len: UM} -> (str, len);
		:ch{ch: C! &} -> (&ch, 1);

		# cmp(rhs: [C!]View #&) SM
		{
			minSz #::= ##THIS > ##rhs ? ##rhs : ##THIS;

			FOR(i:UM := 0; i < minSz; i++)
				IF(THIS[:ok(i)] != rhs[:ok(i)])
					= THIS[:ok(i)] - rhs[:ok(i)];

			= ##THIS - ##rhs;
		}

		# starts_with(find: [C!#]View #&) BOOL
		{
			IF(##find > ##THIS)
				= FALSE;
			= mem::cmp(THIS.Data, find.Data, ##find) == 0;
		}

		# THIS>=(find: [C!#]View #&) BOOL INLINE := starts_with(find);

		# THIS==(rhs: [C!]View #&) BOOL
		{
			IF(##THIS != ##rhs)
				= FALSE;

			FOR(i:UM := 0; i < ##THIS; i++)
				IF(THIS[:ok(i)] != rhs[:ok(i)])
					= FALSE;

			= TRUE;
		}

		# THIS!=(rhs: [C!]View #&) BOOL INLINE
			:= !(THIS == rhs);
	}

	TYPE CV := [CHAR#]View;
	TYPE V := [CHAR]View;

	[C: TYPE]
	view(str: C! *) C!-View INLINE := str;

	[C: TYPE]
	len(str: C! #*) UM
	{
		IF(!str)
			= 0;
		i: UM := 0;
		WHILE(str[i]) ++i;
		= i;
	}
}