(/rl/)

::std [T: TYPE; V: TYPE]
Iterator
{
	PROTECTED Elems: V! \;
	PROTECTED Current: UM;

	{elems: V! \, start: UM}:
		Elems(elems),
		Current(start);

	# get() T!& -- := (*Elems)[Current];
	# THIS! T!& -- := (*Elems)[Current];
	# ok() [T!; V!]OkIterator -- := (Elems, Current);

	next() VOID
	{
		ASSERT(Current < ##*Elems);
		++Current;
	}

	++THIS THIS& -- { next(); = THIS; }
	THIS++ THIS -- { v ::= THIS; next(); = v; }
	--THIS THIS& -- { prev(); = THIS; }
	THIS-- THIS -- { v ::= THIS; prev(); = v; }

	# *THIS T!& -- := get();

	# THIS+(offset: UM) THIS
	{
		ASSERT(Current+offset <= ##*Elems);
		= (Elems, Current + offset);
	}

	# ##THIS UM -- := ##*Elems-Current;
	# THIS() UM -- := Current;

	THIS += (offset: UM) VOID
	{
		ASSERT(Current + offset <= ##*Elems);
		Current += offset;
	}

	prev() VOID
	{
		ASSERT(~Current);
		--Current;
	}

	# <BOOL> -- := Current < ##*Elems;
}

::std [T: TYPE; V: TYPE]
OkIterator -> [T!; V!]Iterator
{
	{elems: V! \, start: UM} -> (elems, start);

	next() VOID -- { ++THIS.Current; }
	++THIS THIS& -- { next(); = THIS; }
	THIS++ THIS -- { v ::= THIS; next(); = v; }

	prev() VOID -- { --THIS.Current; }
	--THIS THIS& -- { prev(); = THIS; }
	THIS-- THIS -- { v ::= THIS; prev(); = v; }

	# get() T!& -- := (*THIS.Elems)[:ok(THIS.Current)];
	# THIS! T!& -- := (*THIS.Elems)[:ok(THIS.Current)];
	# *THIS T!& -- := get();

	# THIS+(offset: UM) THIS --
		:= (THIS.Elems, THIS.Current + offset);

	THIS += (offset: UM) VOID --
		{ THIS.Current += offset; }
}

(/
TEST "parallel for" {
	v: INT-std::Vec(20);
	FOR||(it ::= v.start().ok())
		*it := 5;

	FOR(it ::= v.start().ok(); it; it += SIMD)
	||{ ##it } {
		*(it + IOTA) := 5;
	}
}


	(//
	A := &v;
	B := v.data;
	C := v.length
	D := 0;
.for:
	IF D < C
		JMP .end_for
	D := v.length - 0;
	SIMD D
	E := E + I

.end_for:
	/)
/)