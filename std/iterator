(/rl/)

::std [V: TYPE]
Iterator
{
	PRIVATE Elems: V! \;
	PRIVATE Current: UM;

	{elems: V! \, start: UM}:
		Elems(elems),
		Current(start);

	# get() INLINE & ::= (*Elems)[Current];
	# THIS! INLINE & ::= (*Elems)[Current];
	# ok() INLINE [V!]OkIterator := (Elems, Current);

	next() VOID
	{
		ASSERT(Current < ##*Elems);
		++Current;
	}

	++THIS INLINE [V!]Iterator& { next(); = THIS; }
	THIS++ INLINE [V!]Iterator { v ::= THIS; next(); = v; }
	--THIS INLINE [V!]Iterator& { prev(); = THIS; }
	THIS-- INLINE [V!]Iterator { v ::= THIS; prev(); = v; }

	# *THIS INLINE & ::= get();

	# THIS+(offset: UM) [V!]Iterator
	{
		ASSERT(Current+offset < ##*Elems);
		= (Elems, Current + offset);
	}

	prev() VOID
	{
		ASSERT(~Current);
		--Current;
	}

	# <BOOL> INLINE := Current < ##*Elems;
}

::std [V: TYPE]
OkIterator
{
	PRIVATE Elems: V! \;
	PRIVATE Current: UM;

	{elems: V! \, start: UM}:
		Elems(elems),
		Current(start);

	# <[V!]Iterator> INLINE := (Elems, Current);

	# get() & ::= (*Elems)[Current];
	# THIS! & ::= (*Elems)[Current];

	next() INLINE VOID
	{ ++Current; }

	++THIS INLINE [V!]OkIterator& { next(); = THIS; }
	THIS++ INLINE [V!]OkIterator { v ::= THIS; next(); = v; }
	--THIS INLINE [V!]OkIterator& { prev(); = THIS; }
	THIS-- INLINE [V!]OkIterator { v ::= THIS; prev(); = v; }

	# *THIS INLINE & ::= get();

	# THIS+(offset: UM) INLINE [V!]OkIterator
		:= (Elems, Current + offset);

	prev() INLINE VOID
	{ --Current; }

	# <BOOL> INLINE := Current < ##*Elems;
}