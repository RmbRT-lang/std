(/rl/)

INCLUDE 'std/help'
INCLUDE 'std/memory'

::std [T: TYPE] Shared
{
	PRIVATE Alloc
	{
		P: T! *;
		Refs: UM;
		{p: T! *, refs: UM}:
			P(p), Refs(refs);

		(// Returns the address of the wrapped object. /)
		# ptr() INLINE T! * := P ? P : <T! *>(<UM>(&THIS) + SIZEOF(#THIS));

		ref() INLINE VOID { ++Refs; }

		deref() VOID 
		{
			IF(!Refs)
				THROW;

			IF(!--Refs)
			{
				ptr()->~;
				IF(P)
					heap::free(P);
				heap::free(&THIS);
			}
		}
	}

	PRIVATE P: Alloc *;

	{}: P(NULL);
	{NULL}: P(NULL);
	{p: T! *, :transfer}: P([Alloc]new(p, 1, FALSE));
	{&&mv}: P(mv.P) { mv.{}; }
	{#&cpy}: P(cpy.P) { IF(P) P->ref(); }
	PRIVATE {alloc: [T!]Shared::Alloc *}: P(alloc);

	{:create}:P(alloc_impl()) { get()->{}; }

	[A...:TYPE]
	{:create, a: A!&&...}: P(alloc_impl())
	{ get()->{<A!&&>(a)...}; }

	DESTRUCTOR { IF(P) P->deref(); }

	# <T! *> INLINE := THIS!;
	# THIS! INLINE T! * := P ? P->ptr() : NULL;
	# <BOOL> INLINE := P != NULL;

	# get() T! * { ASSERT(P != NULL); = P->ptr(); }
	# get(:ok) T! * := P->ptr();

	# *THIS T! &
	{
		ASSERT(P != NULL);
		= *P->ptr();
	}

	PRIVATE STATIC alloc_impl() Alloc \
	{
		ret ::= <Alloc \>(heap::alloc(SIZEOF(Alloc) + SIZEOF(T!)));
		ret->{NULL, 1};
		= ret;
	}
	STATIC alloc() [T!]Shared := alloc_impl();
}