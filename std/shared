(/rl/)

INCLUDE 'std/memory'
INCLUDE 'std/dyn'

::std [T: TYPE] SharedOpt
{
	PRIVATE Ptr: T! *;
	PRIVATE Container: detail::SharedAlloc *;

	{}: Container(NULL), Ptr(NULL);
	{BARE} (NULL);
	{NULL}: Container(NULL), Ptr(NULL);
	{&&mv}: Container(mv.Container), Ptr(mv.Ptr) { mv.{}; }

	{#&cpy}: Container(cpy.Container), Ptr(cpy.Ptr)
	{ IF(Container) Container->ref(); }

	# is_unique() BOOL INLINE := !Ptr || Container->Refs == 1 && !Container->Weaks;

	clone_if_shared() VOID
	{
		IF(is_unique()) RETURN;

		offset ::= <UM>(Ptr) - <UM>(Container->obj_base_addr());

		Container->Refs--;
		Container := heap::clone_virtual_p(Container);

		Container->Refs := 1;
		Container->Weaks := 0;
		Ptr := <T! *>(<UM>(Container->obj_base_addr()) + offset);
	}

	:gc{p: T! *}: Ptr(p), Container(NOINIT)
	{
		IF(p)
			Container := heap::[T-detail::SplitAlloc]new(:gc(p));
		ELSE
			Container := NULL;
	}

	:share{v: T!-std::Dyn}:
		Ptr := v.ptr(),
		Container := heap::[T-detail::SplitAlloc]new(:share(&&v))
	{
		ASSERT(Ptr);
	}

	[A...:TYPE]
	:make{a: A!&&...} := :share(:make(<A!&&>(a)...));

	[A...:TYPE]
	:make_if{if: BOOL, a: A!&&...} := :share(:make_if(if, <A!&&>(a)...));

	[A...:TYPE]
	:if{if: BOOL, a: A!&&...} { IF(if) THIS := :a(<A!&&>(a)...); }

	[A...:TYPE]
	:a{a: A!&&...}:
		Container(heap::[T-detail::CompactAlloc]new(<A!&&>(a)...)),
		Ptr(NOINIT)
	{
		Ptr := &<T-detail::CompactAlloc *>(Container)->Value;
	}

	[U:TYPE; A...:TYPE] :a.U!{a: A!&&...} := :share(:a.U!(<A!&&>(a)...));

	[U:TYPE] :dup{rhs: U!&&}
	{
		alloc: [U-detail::CompactAlloc]Dyn := :a(<U!&&>(rhs));
		Ptr := <<T \>>(&<U-detail::CompactAlloc *>(alloc.ptr())->Value);
		Container := alloc.release();
	}
	[U:TYPE] {rhs: U!-SharedOpt} INLINE := :<>(rhs);
	[U:TYPE] :<>{rhs: U!-SharedOpt} INLINE
	{
		IF(rhs.ptr())
		{
			Ptr := <<T! \>>(rhs.ptr());
			Container := rhs.container();
			rhs.{};
		}
	}

	[U:TYPE] :try{rhs: U!-SharedOpt} INLINE
	{
		IF(rhs)
			IF(Ptr := <<T! *>>(rhs))
			{
				Container := rhs.container();
				rhs.{};
			}
	}

	DESTRUCTOR { IF(Container) Container->deref(); Container := NULL; }

	[Args...: TYPE]
	ensure(args: Args!&&...) T! & INLINE
	{
		IF(!THIS)
			THIS.{:a(<Args!&&>(args)...)};
		= *Ptr;
	}

	# ptr() T! * INLINE := Ptr;
	# <T! *> INLINE := Ptr;
	# THIS! T! & INLINE := *THIS;
	# *THIS T! & INLINE { ASSERT(Ptr); = *Ptr; }
	# THIS() T! & INLINE := *THIS;
	# container() detail::SharedAlloc * := Container;

	# THIS<>(rhs: T! # \) S1 := ptr() == rhs ?? 0 : THIS() <> *rhs;
}

::std [T:TYPE] Shared -> [T!]SharedOpt
{
	[A...:TYPE] { a: A!&&... } -> (:a, <A!&&>(a)...);
	[A...:TYPE] :a{ a: A!&&... } -> (:a, <A!&&>(a)...);
	[A...:TYPE] :make{ a: A!&&... } -> (:make, <A!&&>(a)...);
	[U:TYPE] :dup{rhs: U!&&} -> (:dup, <U!&&>(rhs));
	[U:TYPE; A...:TYPE] :a.U!{ a: A!&&... } -> (:a.U!, <A!&&>(a)...);
	[U:TYPE] :<>{ rhs: U!-std::Shared } -> (:<>, &&rhs);
	[U:TYPE] :!{ rhs: U!-SharedOpt } -> (:<>, &&rhs) { ASSERT(THIS); }
	:share{v: T!-std::Dyn} -> (:share, &&v);
	[U:TYPE] :gc{p: U! \} -> (:gc, p);

	{BARE} -> (BARE);
	{#&};
	{&&};
}

::std [T:TYPE] Weak
{
	PRIVATE Container: detail::SharedAlloc *;
	PRIVATE Ptr: T! *;

	{}: Container(NULL), Ptr(NULL);
	{BARE} ();
	{NULL}: Container(NULL), Ptr(NULL);
	{&&mv}: Container(mv.Container), Ptr(mv.Ptr) { mv.{}; }
	
	{#&cpy}: Container(cpy.Container), Ptr(cpy.Ptr)
	{ IF(Container) Container->ref_weak(); }

	:weak{v: [T!]Shared #&}: Container(v.container()), Ptr(v.ptr())
	{
		IF(Container)
			Container->ref_weak();
	}

	[U:TYPE] :<>{rhs: U!-SharedOpt #&} INLINE
	{
		IF(rhs)
		{
			Ptr := <<T! \>>(rhs.ptr());
			Container := rhs.container();
			Container->ref_weak();
		}
	}

	[U:TYPE] :try{rhs: U!-SharedOpt #&} INLINE
	{
		IF(rhs)
			IF(Ptr := <<T! *>>(rhs.ptr()))
			{
				Container := rhs.container();
				Container->ref_weak();
			}
	}

	DESTRUCTOR { IF(Container) Container->deref_weak(); }

	# ptr() T! * INLINE := Container && Container->Refs ?? Ptr : NULL;

	# <T! *> INLINE := ptr();
	# THIS! T! & INLINE := *THIS;
	# *THIS T! & INLINE { p ::= ptr(); ASSERT(p); = p; }
	# container() detail::SharedAlloc * := Container;

	# THIS<>(rhs: T! # \) S1 := THIS() <> *rhs;
}

::std::detail SharedAlloc VIRTUAL
{
	Refs: U2;
	Weaks: U2;

	{}: Refs(1);
	{BARE} ();

	ref() VOID INLINE { ASSERT(Refs); ASSERT(++Refs); }
	ref_weak() VOID INLINE { ASSERT(++Weaks); }

	# ABSTRACT obj_base_addr() VOID #\;

	deref() VOID
	{
		ASSERT(Refs);
		IF(!--Refs)
		{
			THIS.~;
			IF(!Weaks)
				heap::free(&THIS);
		}
	}

	deref_weak() VOID
	{
		ASSERT(Weaks);
		IF(!--Weaks && !Refs)
			heap::free(&THIS);
	}
}

::std::detail [T:TYPE] CompactAlloc -> SharedAlloc
{
	Value: T;

	[A...:TYPE]{a: A!&&...}: Value(<A!&&>(a)...);

	# FINAL obj_base_addr() VOID #\ := &&&Value;
}

::std::detail [T:TYPE] SplitAlloc -> SharedAlloc, PRIVATE [T]Dyn
{
	:gc{ptr: T! \}-> (), (:gc(ptr));
	:share{v: T!-std::Dyn} -> (), (&&v);

	# FINAL obj_base_addr() VOID #\ := &&&*THIS;
}