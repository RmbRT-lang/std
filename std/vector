(/rl/)

INCLUDE 'std/range'
INCLUDE 'std/dyn'
INCLUDE 'std/shared'
INCLUDE 'std/value'
INCLUDE 'std/memory'
INCLUDE 'std/math/limit'
INCLUDE 'std/iterator'
INCLUDE 'std/hash'

::std [T: TYPE] Vec -> CustomHashable0Cost
{
PRIVATE:
	Content: [T!]Buffer;
	PRIVATE #? Capacity() U4 #? & -- := Content.Unused;
PUBLIC:
	{} -- { Capacity() := 0; }
	{BARE} -- ();

	:noinit{size: U4}:
		Content := heap::[T]alloc_array(size)
	{
		Capacity() := size;
	}

	{size: U4} := :noinit(size)
	{
		range::default_construct(Content.Data, ##Content);
	}
	:bare{size: U4} := :noinit(size)
	{
		FOR(i ::= 0; i < size; i++)
			Content.Data[i].{BARE};
	}
	:reserve{capacity: U4}:
		Content(heap::[T]alloc_array(capacity))
	{
		Capacity() := capacity;
		Content.Size := 0;
	}

	{#&copy}:
		Content(heap::clone_buf(copy.Content))
		{ Capacity() := ##copy; }
	{&&move}:
		Content(move.Content)
	{
		move.{};
	}

	:copy{content: [T#]Buffer #&}:
		Content(heap::clone_buf(content))
		{ Capacity() := ##content; }
	:move{content: [T!]Buffer &&}:
		Content(content)
	{
		Capacity() := ##content;
		content := NULL;
	}

	[A...:TYPE]
	:vec{a: A!&&...} := :reserve(SIZEOF...(A!))
	{ (THIS += <A!&&>(a))...; }

	DESTRUCTOR --
	{
		IF(Content.Data)
			heap::delete_buf(Content);
	}

	erase(at: U4) VOID
	{
		ASSERT(at < ##THIS);

		THIS![:ok(at)].~;
		--Content.Size;
		range::move_construct(&THIS!.Data[at], &THIS!.Data[at+1], ##THIS-at);
	}

	pop_back(dst: T *) VOID
	{
		ASSERT(!empty());

		IF(dst)
			*dst := &&THIS![:ok(##THIS-1)];
		THIS![:ok(##THIS-1)].~;
		--Content.Size;
	}

	append_move(rhs: T!-std::Buffer) VOID
	{
		cap: U4;
		IF(!Capacity())
			cap := ##rhs;
		ELSE IF(Capacity() > ##THIS + ##rhs)
			cap := Capacity();
		ELSE
			cap := (##THIS + ##rhs + (Capacity() >> 4)) | 3;

		reserve(cap);

		FOR(it ::= rhs.start().ok(); it; it++)
			THIS += &&it!;
	}

	append(rhs: T!#-std::Buffer) VOID
	{
		cap: U4;
		IF(!Capacity())
			cap := ##rhs;
		ELSE IF(Capacity() > ##THIS + ##rhs)
			cap := Capacity();
		ELSE
			cap := (##THIS + ##rhs + (Capacity() >> 4)) | 3;

		reserve(cap);

		FOR(it ::= rhs.start().ok(); it; it++)
			THIS += it!;
	}

	(//
		Reserve the requested amount of space, and leave an uninitialised hole
		in the contents at the requested location. If the hole is within the
		contents, increases the container size to accomodate the hole.
	/)
	PRIVATE reserve_with_hole_at(cap: U4, at: U4) BOOL
	{
		ASSERT(at <= ##Content);
		ASSERT(at <= cap);

		/// Make sure there is space for the hole.
		IF(at < ##Content && cap == ##Content)
			++cap;

		/// Is the container already large enough?
		IF(Capacity() >= cap)
			/// Grow hole if required.
			IF(at < ##Content)
			{
				Content.Data[##Content].{&&Content.back()};
				failed ::= TRUE;
				/// Shift contents right.
				TRY
				{
					range::move_assign_r(
						Content.Data+at+1,
						Content.Data+at,
						##Content-(at+1));
					failed := FALSE;
				} FINALLY IF(failed)
				{
					Content.back() := &&Content.Data[##Content];
					Content.Data[##Content].~;
				}

				Content[at].~; /// Destroy hole.
				++Content.Size; /// Adjust size.

				= TRUE;
			} ELSE
				= FALSE;

		/// Reserve enough space.
		hasHole ::= at != ##Content;
		new ::= heap::[T]alloc_array(cap).cut(##Content + hasHole);

		range::move_construct(new.Data, Content.Data, at);
		range::move_construct(new.Data+at+1, Content.Data+at, ##Content-at);

		IF(Content.Data)
			heap::delete_buf(Content);
		Content := new;
		Capacity() := cap;
		= hasHole;
	}

	(// Reserve space for the requested capacity, without shrinking. /)
	reserve(cap: U4) VOID
	{
		reserve_with_hole_at(cap, ##Content);
	}

	resize(size: U4) VOID
	{
		IF(size >= ##Content)
		{
			reserve(size);
			range::default_construct(
				&THIS[:ok(##THIS)],
				size - ##Content);
		}
		ELSE
			range::destroy_buf(Content.drop_start(size));
		Content.Size := size;
	}

	resize_bare(size: U4) VOID
	{
		IF(size >= ##Content)
		{
			reserve(size);
			range::bare_construct(
				&THIS[:ok(##THIS)],
				size - ##Content);
		}
		ELSE
			range::destroy_buf(Content.drop_start(size));
		Content.Size := size;
	}

	(//
		Reserve space for at least one more entry than is currently held.
		Increases ##Content. Leaves an uninitialised hole at the requested
		location.
	/)
	PRIVATE grow_reserve_with_hole_at(at: U4) VOID
	{
		cap: U4;
		IF(!Capacity())
			cap := 1;
		ELSE IF(Capacity() > ##THIS)
			cap := Capacity();
		ELSE
			cap := (Capacity()+1 + (Capacity() >> 4)) | 3;

		/// Increase content if appending.
		Content.Size += !reserve_with_hole_at(cap, at);
	}

	[A...:TYPE]
	emplace_at(at: U4, a: A!&&...) T! & --
	{
		grow_reserve_with_hole_at(at);
		failed ::= TRUE;
		TRY
		{
			elem:?& := THIS[at];
			elem.{<A!&&>(a)...};
			failed := FALSE;
			= elem;
		} FINALLY IF(failed)
		{
			IF(at != ##THIS-1)
			{
				THIS[at].{&&THIS[at+1]};
				range::move_assign(&THIS[at+1], &THIS[at+2], ##THIS-(at+2));
				THIS!.back().~;
			}
			Content.Size--;
		}
	}

	[A:TYPE]
	THIS+=(v: A!&&) T! & --
		:= emplace_at(##THIS, <A!&&>(v));

	#? THIS[i: Index] T!#?& -- := Content[i];

	# <BOOL> := Content.Size;

	# ##THIS U4 -- := Content.Size;
	# data() T! #* -- := Content.Data;
	# capacity() U4 -- := Capacity();
	# empty() BOOL -- := ##THIS == 0;

	#? THIS! ? #?& -- := Content;

	#? start() [T!]Vec#?-[T!#?]Iterator -- := (&THIS, 0);
	#? end() [T!]Vec#?-[T!#?]Iterator -- := (&THIS, ##THIS-1);

	# index_of(ptr: T! #\) U4 -- := ptr - Content.Data;

	# THIS <> (rhs: THIS #&) S1
	{
		len # ::= math::min(##THIS, ##rhs);
		FOR(i ::= 0; i < len; i++)
			IF(sign ::= THIS[:ok(i)] <> rhs[:ok(i)])
				= sign;
		= ##THIS <> ##rhs;
	}

	# hash(h: std::Hasher &) VOID
	{
		h(##THIS);
		FOR(elem ::= start())
			h(*elem);
	}
}

::std [T:TYPE] TYPE DynVec := T! - Dyn - Vec;
::std [T:TYPE] TYPE SharedVec := T! - Shared - Vec;
::std [T:TYPE] TYPE ValVec := T! - Val - Vec;