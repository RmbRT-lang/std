(/rl/)

INCLUDE 'std/iterator'
INCLUDE 'std/range'
INCLUDE 'std/index'

(// A buffer keeps track of allocated memory but does not own it. /)
::std [T: TYPE] Buffer
{
	Data: T! *;
	Size: UM;

	{};
	{NULL};
	{s: T! *, c: UM}: Data(s), Size(c)
	{
		ASSERT(!Size || Data);
	}

	:buf{s: T! *, c: UM} (s, c);

	# THIS++ {:buf, T! *, UM} INLINE := :buf(Data, Size);
	# <BOOL> INLINE := Data != NULL;

	#? THIS[i: Index #&] T!#?& INLINE := Data[i(Size)];

	# range(
		r: Range #&) [T!]Buffer INLINE
	{
		r(Size);
		= (Data + r.Start, r.Size);
	}

	# drop_start(count: Index) [T!]Buffer
		:= range((count[Size], Size - count[Size]));
	# drop_end(count: Index) [T!]Buffer
		:= range((0, Size - count[Size]));
	# cut(size: Index) [T!]Buffer
		:= range((0, size[Size]));
	# cut_start(size: Index) [T!]Buffer
		:= range((Size-size[Size], size[Size]));

	# ##THIS UM INLINE := Size;

	#? front() T!#?& INLINE := THIS[0];
	#? start() [T!]Buffer#?-[T!#?]Iterator INLINE := (&THIS, 0);

	#? back() T!#?& INLINE:= THIS[Size-1];
	#? end() [T!]Buffer#?-[T!#?]Iterator INLINE := (&THIS, Size-1);

	# empty() BOOL INLINE := !Size;
}

::std::range [T: TYPE] destroy_buf(v: [T!]Buffer) VOID INLINE
	:= destroy(v.Data, v.Size);