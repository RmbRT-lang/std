(/rl/)

INCLUDE 'std/memory'
INCLUDE 'std/math/safe'

::std::io::scan
{
	[T:TYPE] dec(s: [CHAR#]Buffer #&, v: T! &) UINT
	{
		v := 0;

		view: str::CV := s++;
		sign ::= view.starts_with("-");
		IF(sign) view := view.drop_start(1)++;

		FOR(i ::= 0; i < ##view; i++)
		{
			c ::= view[i];
			IF(c < '0' || c > '9')
			{
				IF(sign)
					v := math::safe::neg(v);
				= sign ?? i ?? i + 1 : 0 : i;
			}

			v := math::safe::mul(<T>(10), v) + <T>(c-'0');
		}

		IF(sign)
			v := math::safe::neg(v);

		= ##s;
	}

	[T:TYPE] hex(s: [CHAR#]Buffer, v: T! &) UINT
	{
		v := 0;

		view: str::CV := s++;

		sign ::= view.starts_with("-");
		IF(sign) view := view.drop_start(1)++;

		b ::= view.starts_with("0x") || view.starts_with("0X");
		IF(b)
			view := view.drop_start(2)++;

		FOR(i ::= 0; i < ##view; i++)
		{
			d: T;
			c ::= view[i];
			IF(c >= '0' && c <= '9')
				d := c - '0';
			ELSE IF(c >= 'a' && c <= 'f')
				d := c - ('a' - 0xa);
			ELSE IF(c >= 'A' && c <= 'F')
				d := c - ('A' - 0xA);
			ELSE
			{
				IF(sign)
					v := math::safe::neg(v);
				= i ?? (b ?? 2:0) + (sign ??1:0) + i : 0;
			}

			v := math::safe::mul(<T>(16), v) + <T>(d);
		}

		IF(sign)
			v := math::safe::neg(v);

		= ##s;
	}
}