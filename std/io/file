(/rl/)

INCLUDE 'std/err/filenotfound'
INCLUDE 'std/tags'

::std::io
{
	out: File(libc::stdout);
	in: File(libc::stdin);
	err: File(libc::stderr);

	File
	{
		PRIVATE F: libc::FileHandle \;
		NoCopy;

		{h: libc::FileHandle \}: F(h)
		{
			ASSERT(F);
		}
		write_some(d: VOID #\, s: UM) UM -- := libc::fwrite(d, 1, s, F);
		read(d: VOID \, s: UM) UM -- := libc::fread(d, 1, s, F);
		read_some(d: VOID \, s: UM) UM -- := libc::fread(d, 1, s, F);
		flush() INT -- := libc::fflush(F);
		eof() BOOL -- := libc::feof(F);
		line(d: CHAR \, s: UM) CHAR * -- := libc::fgets(d, s, F);

		STATIC remove(file: CHAR #\) BOOL := 0 == libc::remove(file);

		DESTRUCTOR { libc::fclose(F); }
		{file: CHAR #\, mode: CHAR#\}:
			F(libc::fopen(file, mode))
		{
			IF(!F)
				THROW <FileNotFound>(<std::str::CV>(file));
		}

		:read{file: CHAR#\} := (file, "rb");
		:write{file: CHAR#\} := (file, "wb");

		print(str: CHAR #\) ? := libc::fputs(str, F);
	}
}

::std::io::libc
{
	FileHandle {}
	EXTERN fopen(CHAR#\,CHAR#\) FileHandle *;
	EXTERN fclose(FileHandle *) VOID;
	EXTERN fwrite(VOID#\, UM, UM, FileHandle *) UM;
	EXTERN fread(VOID\, UM, UM, FileHandle *) UM;
	EXTERN feof(FileHandle *) INT;
	EXTERN ferror(FileHandle *) INT;
	EXTERN fflush(FileHandle \) INT;
	EXTERN fputs(CHAR #\, FileHandle \) INT;
	EXTERN fgets(CHAR\, INT, FileHandle \) CHAR *;
	EXTERN remove(CHAR#\) INT;

	EXTERN stdin: FileHandle *;
	EXTERN stdout: FileHandle *;
	EXTERN stderr: FileHandle *;
	EXTERN errno: INT;
}