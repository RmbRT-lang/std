(/rl/)

INCLUDE 'std/nodestruct'
INCLUDE 'std/hashable'
INCLUDE 'std/hash'

::std [T: TYPE] Opt -> std::CustomHashable0Cost
{
PRIVATE:
	Exists: BOOL;
	Obj: T-NoDestruct;
PUBLIC:

	{} (NULL);
	{BARE} ();
	{NULL}: Obj(NOINIT);

	[Args...:TYPE]
	:a{args: Args!&&...}: Exists(TRUE), Obj(<Args!&&>(args)...);

	[Args...:TYPE]
	:if{cond: BOOL, args: Args!&&...}:
		Exists := cond,
		Obj(NOINIT)
	{
		IF(cond)
			Obj.{<Args!&&>(args)...};
	}

	DESTRUCTOR { IF(Exists) Obj->~; }
	
	
	{#&rhs}: Exists(rhs.Exists), Obj(NOINIT)
	{
		IF(Exists)
			Obj->{*rhs};
	}
	
	{&&rhs}: Exists(rhs.Exists), Obj(NOINIT)
	{
		IF(Exists)
		{
			Obj->{&&*rhs};
			rhs := NULL;
		}
	}

	[Args...: TYPE]
	ensure(args: Args!&&...) T!& INLINE
	{
		IF(!Exists)
		{
			Obj.{<Args!&&>(args)...};
			Exists := TRUE;
		}
		= *THIS;
	}

	# <BOOL> INLINE := Exists;

	#? THIS! T! #?& INLINE := *THIS;
	#? *THIS T! #?& INLINE
	{
		ASSERT(Exists);
		= *Obj;
	}

	[Ref: TYPE]
	Ok { V: Ref! *; # <Ref! &> INLINE { ASSERT(V); = <Ref! &>(*V); } }

	#? ok() [T! #?]Ok INLINE := Exists ?? &*Obj : NULL;

	#? ptr() T #? * := Exists ?? &*Obj : NULL;

	
	# THIS<>(rhs: THIS #&) S1
	{
		IF(!THIS) = rhs ?? -1 : 0;
		IF(!rhs) = 1;

		= *THIS <> *rhs;
	}

	# hash(h: std::Hasher &) VOID
	{
		IF(!THIS) h(FALSE);
		ELSE h(TRUE, *THIS);
	}
}