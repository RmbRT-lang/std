(/rl/)

INCLUDE 'std/heap'

(// Optionally heap-allocated value.
	Access the value via `*`, `->`, `!`, or `()`. /)
::std [T:TYPE] DynOpt
{
	PRIVATE Ptr: T! *;

	{}: Ptr(NULL);
	{NULL}: Ptr(NULL);
	{BARE} ();

	{&&m}: Ptr(m.release());

	:gc{p: T! *}: Ptr(p);

	[Args...: TYPE]
	:a{args: Args!&&...}:
		Ptr(heap::[T]new(<Args!&&>(args)...));
	[Arg: TYPE]
	:if{if: BOOL, then: Arg!&&}:
		Ptr := if ?? heap::[T]new(<Arg!&&>(then)) : NULL;

	[Args...: TYPE]
	:make{args: Args!&&...}
		(<<<T>>>(<Args!&&>(args)...));
	[Args...: TYPE]
	:gcmake{args: Args!&&...}:
		Ptr := <<<T>>>(<Args!&&>(args)...);


	[Args...: TYPE]
	:make_if{if: BOOL, args: Args!&&...}:
		Ptr := NOINIT
	{
		IF(if)
			THIS.{<<<T>>>(<Args!&&>(args)...)};
		ELSE
			Ptr := NULL;
	}

	[Args...: TYPE]
	:gcmake_if{if: BOOL, args: Args!&&...}:
		Ptr := if ?? <<<T>>>(<Args!&&>(args)...) : NULL;

	[V:TYPE]
	:dup{v: V!&&}:
		Ptr(heap::[V]new(<V!&&>(v)));

	[V:TYPE] {m: V!-Dyn! &&}
	{
		IF(m)
		{
			// Try to convert into our pointer type, this might fail.
			Ptr := <<T \>>(m!);
			m.{};
		}
		ELSE
			Ptr := NULL;
	}

	# <T! *> INLINE := Ptr;

	# *THIS T! & INLINE { ASSERT(Ptr); = *Ptr; }
	# THIS! T! & INLINE := *THIS;
	# THIS() T! & INLINE := *THIS;

	release() T! *
	{
		p ::= Ptr;
		Ptr := NULL;
		= p;
	}

	[Args...: TYPE]
	ensure(args: Args!&&...) T! * INLINE
	{
		IF(!Ptr)
			Ptr := heap::[T]new(<Args!&&>(args)...);
		= Ptr;
	}

	# THIS<>(rhs: THIS #&) S1
	{
		IF(!THIS) = rhs ?? -1 : 0;
		IF(!rhs) = 1;

		= *THIS <> *rhs;
	}

	DESTRUCTOR
	{
		IF(Ptr)
			heap::delete(Ptr);
	}
}

(// Behaves like a normal instance but is placed on the heap.
	Copying creates an allocated copy, but moving will just transfer ownership of the pointer. After moving, the object's value is invalid until re-initialised. Use `*`, `->`, `!` or `()` to access the object's value. `BARE` initialisation will not allocate an instance. /)
::std [T:TYPE] Dyn
{
	PRIVATE Value: T - std::DynOpt;

	{} INLINE: Value := :a();
	{BARE} INLINE: Value := NULL;
	{#&cpy} INLINE: Value := :a(cpy!);
	{&&};

	:gc{p: T! \}: Value := :gc(p) { ASSERT(p); }

	[Args...: TYPE]
	:make{args: Args!&&...}:
		Value := :make(<Args!&&>(args)...);
	[Args...: TYPE]
	:gcmake{args: Args!&&...}:
		Value := :gcmake(<Args!&&>(args)...);

	[Args...:TYPE]
	{args: Args!&&...} INLINE: Value := :a(<Args!&&>(args)...);

	#? THIS! T#?& INLINE := *Value;
	#? *THIS T#?& INLINE := *Value;
	# THIS() T#?& INLINE := *Value;

	[Args...: TYPE]
	/// Create the internal value if the object is currently invalid.
	ensure(args: Args!&&...) T! & INLINE
		:= Value.ensure(<Args!&&>(args)...);

	/// Detect invalid state after moving. If invalid, any operation besides checking validity is forbidden.
	# valid() BOOL INLINE := Value;
	# <T *> := Value;

	# THIS <> (rhs: THIS #&) S1 := THIS() <> rhs();
}