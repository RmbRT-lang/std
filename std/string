(/rl/)

INCLUDE 'std/memory'

INCLUDE 'std/str'

/// Dynamic container for an std::str::View.
::std [C: TYPE] String
{
	PRIVATE Content: [C!]Buffer;

	{};
	{BARE} ();

	{data: str::[C#]View} --: Content(heap::clone_buf(data));
	:cstring{data: str::[C#]View #&}: Content(heap::[C]alloc_array(##data+1))
	{
		range::copy_construct(Content.Data, data.Data, ##data);
		Content.Data[--Content.Size] := 0;
	}
	:transfer{data: [C!]Buffer #&}: Content(data);

	{#&cpy}:
		Content(heap::clone_buf(cpy.Content));
	{&&mv}:
		Content(mv.Content)
	{
		mv.Content := NULL;
	}
	DESTRUCTOR
	{
		IF(Content.Data)
			heap::free_buf(Content);
		Content := NULL;
	}

	# THIS <> (rhs: THIS#&) S1 := THIS! <> rhs!;

	# data() ? -- := Content.Data;
	# ##THIS ? -- := ##Content;
	# THIS[i: Index #&] ?#& -- := Content[i];
	# THIS! str::[C#]View -- := Content++;

	[T...: TYPE]
	append(v: T!&&...) VOID --
	{ append_impl(<str::[C#]View>(<T!&&>(v))...); }
	[T...: TYPE]
	PRIVATE append_impl(v: T!&&...) VOID {
		sz: UM := ##THIS;
		(sz += ##v)...;

		new ::= heap::[C]alloc_array(sz);
		start ::= new.Data;
		start += range::copy_construct(start, Content.Data, ##Content);
		(start += range::copy_construct(start, v.Data, ##v))...;
		THIS := :transfer(new);
	}

	THIS+=(str: std::str::[C#]View #&) VOID --
	{ append(str); }
}

::std
{
	TYPE Str := [CHAR]String;
	TYPE Str2 := [U2]String;
	TYPE Str4 := [U4]String;
}