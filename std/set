(/rl/)

INCLUDE 'std/vector'
INCLUDE 'std/dyn'

::std [T: TYPE] MASK Set
{
	(// Queries whether a value is in the set. /)
	# has(key: T! #&) BOOL;

	(// Inserts a value into the set.
	@return
		Whether the value was not in the set yet. /)
	insert(v: T! #&) BOOL;

	(// Inserts a value into the set.
	@return
		Whether the value was not in the set yet. /)
	insert(v: T! &&) BOOL;

	(// Removes a value from the set.
	@return
		Whether the value was in the set. /)
	remove(v: T! #&) BOOL;
}

::std [T: TYPE] VecSet
{
	PRIVATE Elems: [T!]Vec;

	TYPE Location := UM;

	{};
	:reserve{cap: UM}: Elems := :reserve(cap);

	#? start() ? INLINE := Elems.start();
	#? end() ? INLINE := Elems.end();
	# empty() BOOL INLINE := Elems.empty();
	#? at_loc(l: Location) T#? * INLINE := l >= ##Elems ?? NULL : &Elems[:ok(l)];

	# ##THIS UM INLINE := ##Elems;

	[V: TYPE]
	# has (v: V! #&) BOOL INLINE := find(v);

	[V: TYPE]
	#? find(v: V! #&) T! #? -OptRef
	{
		dummy: Location;
		= find_loc2(v, &dummy);
	}

	[V: TYPE]
	#? find_loc(v: V! #&) { T! #?-OptRef, Location }
	{
		loc: Location;
		ret ::= find_loc2(v, &loc);
		= (ret, loc);
	}

	[V: TYPE]
	#? find_loc2(
		v: V! #&,
		location: Location \
	) T! #? *
	{
		l: UM := 0;
		r: UM := ##Elems;

		i: UM := 0;
		WHILE(l < r)
		{
			i := (l + r)>>1;
			IF:!(sign ::= Elems[i] <> v)
			{
				*location := i;
				= &Elems[i];
			}

			IF(sign > 0)
				r := i;
			ELSE
				l := i+1;
		}

		*location := l;
		= NULL;
	}

	insert(v: T!) BOOL
	{
		loc: Location;
		IF(find_loc2(v, &loc))
			= FALSE;
		emplace_at(loc, &&v);
		= TRUE;
	}

	[A:TYPE]
	THIS+=(v: A!&&) BOOL INLINE
		:= THIS.insert(<A!&&>(v));

	remove_at(
		position: Location) VOID INLINE
	{
		Elems.erase(position);
	}

	[V: TYPE]
	remove(v: V! #&) BOOL
	{
		loc: Location;
		IF(find_loc2(v, &loc))
		{
			remove_at(loc);
			= TRUE;
		}
		= FALSE;
	}

	THIS-=(v: T! #&) BOOL INLINE := THIS.remove(v);

	[V: TYPE]
	pop(v: V! #&, deleted: T! \) BOOL
	{
		loc: Location;
		IF(it ::= find_loc2(v, &loc))
		{
			*deleted := &&*it;
			remove_at(loc);
			= TRUE;
		}
		= FALSE;
	}


	[A...:TYPE]
	emplace_at(
		position: Location,
		a: A!&&...) T! &
	{
		Elems.emplace_at(position, <A!&&>(a)...);
		= Elems[position];
	}
}

::std [T:TYPE] TYPE DynVecSet := [T!-std::Dyn+]VecSet;
